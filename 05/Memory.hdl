// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
        
    // RAM16K : 000..000 - 011..111 -> address[14]=0 であれば RAM16K
    // Screen : 100..000 - 101..111-> address[14]=1 かつ address[13]=0 であれば Screen
    // Keyboard : 110..000 -> address[14]=1 かつ address[13]=1 であれば Keyboard
    
    // 指定されたloadを、アクセスするほう(RAMかScreenのどちらか)の入力値に渡す必要がある
    // これをやらないと、例えば load=1でRAMにアクセスする時、Screenの文でもload=1を指定してしまい、その次のタイムユニットで Screen[address]の値を書き換えてしまう
    DMux(in=load, sel=address[14], a=loadr, b=loadsk);
    DMux(in=loadsk, sel=address[13], a=loads, b=loadk);
    
    // それぞれにアクセス
    RAM16K(in=in, load=loadr, address=address[0..13], out=outr);
    Screen(in=in, load=loads, address=address[0..12], out=outs);
    Keyboard(out=outk);
    
    // outr,outs,outk の中で、今回アクセスしているものの出力値をoutとして出力する
    Mux16(a=outs, b=outk, sel=address[13], out=outsk);
    Mux16(a=outr, b=outsk, sel=address[14], out=out);
}